//!precedence

@precedence {
  member,
  newArgs,
  call,
  times @left,
  plus @left,
  rel @left,
  ternary @right,
  assign @right,
  wait_block,
  forIn,
  else,
  range @left
  statement @cut
}


@top Project { project_block* }

project_block {
  GlobalBlock |
  MainBlock |
  ProgramBlock |
  ConditionBlock |
  FunctionBlock | 
  ImportBlock
}

GlobalBlock  { kw<"shared"> "{" shared_declaration* "}" }
MainBlock    { kw<"main"> code_block }
ProgramBlock { kw<"program"> ProgName ArgList code_block }
ConditionBlock { Condition expression_block }
FunctionBlock { kw<"fn"> FnName ArgList ReturnType code_block }
ImportBlock { kw<"import"> "[" ImportList "]" }

ImportList { ImportItem ("," ImportItem)* ","? }
ImportItem { ImportPath (kw<"as"> ImportAlias)? }
ImportPath { identifier (ImportSlash identifier)* }
ImportAlias { identifier }

ReturnType {"->" Type}

Condition {
  kw<"always"> |
  kw<"eventually">
}

code_block {
  !statement "{" statement* "}"
}
expression_block {
  !statement "{" (expression ";")* "}"
}

shared_declaration { 
  (kw<"let"> | kw<"const">)
  SharedVariableName (":" Type)? ("=" expression)? semi }

statement {
  VariableDeclaration |
  ChannelDeclarationStatement |
  WhileStatement |
  ForStatement |
  IfStatement |
  run_statement |
  LoopStatement |
  AtomicStatement |
  send_statement |
  wait_statement |
  code_block |
  call_statement | 
  assignment_statement |
  return_statement |
  break_loop_statement
}


assignment_statement {
  AssignmentExpression semi
}
AssignmentExpression {
  Variable !assign UpdateOp expression
}
Variable { LocalVariableName | SharedVariableName | kw<"self"> }


//!return_statement

return_statement { kw<"return"> expression? semi}


//!break_loop_statement
break_loop_statement { (kw<"break"> | kw<"continue">) identifier? semi}

//!assign

//!variabledeclaration

VariableDeclaration {
  (kw<"let"> | kw<"const">)
  LocalVariableName (":" Type)? ("=" expression)? semi
}

//!ChannelDeclarationStatement

ChannelDeclarationStatement {
  kw<"channel"> object_identifier "<"? "(" commaSep<Type> ")" ">"? object_identifier ";"
}

//!whilestatement

WhileStatement {
  kw<"while"> expression code_block
}
//!ForStatement

ForStatement {
  kw<"for"> LocalVariableName kw<"in"> ListExpression code_block
}

ListExpression  { RangeExpression }
RangeExpression  {
	expression !range ("..") expression
}


//!LoopStatement

LoopStatement {
  kw<"loop"> statement
}

//!AtomicStatement

AtomicStatement {
  (kw<"atomic"> | LogicOp<"!">) statement
}


//!ifstatement

IfStatement {
  kw<"if"> expression code_block (!else kw<"else"> code_block)?
}

//!run_statement

run_statement {
   RunExpression ";"
}
RunExpression { kw<"run"> ProgName ArgList }

//!send_statement

send_statement {
   SendExpression ";"
}
SendExpression { kw<"send"> object_identifier ArgList }

//!wait_statement
wait_statement  { kw<"await"> (
      WaitingBlock 
    | WaitingBlockCase) }

WaitingBlock {
    (kw<"seq"> | kw<"first">) "{" WaitingBlockCase* "}"
}
WaitingBlockCase { 
   (ReceiveExpression | expression)
   (";" | ("=>" statement)) 
}

ReceiveExpression { kw<"receive"> object_identifier? pattern_list (!wait_block "=>" statement)? }

object_identifier { (Variable "." object_identifier) | Variable }

pattern_list { "(" pattern ("," pattern)* ")" }

pattern { LocalVariableName | Literal }

//!call_statement

call_statement {
   CallExpression ";"
}
CallExpression {
  (FnName | MemberExpression) ArgList
}

MemberExpression {
  // case 1: a.b.c…   (recurse on MemberExpression)
  MemberExpression "." identifier
| // case 2: x.y     (simple identifier)
  identifier "." identifier
| // case 3: (….) . z
  ParenthesizedExpression "." identifier
}


//!expression

expression[@isGroup=Expression] {
  Number |
  String |
  Variable ~arrow |
  @specialize[@name=BooleanLiteral]<non_global_identifer, "true" | "false"> |
  kw<"null"> |
  RegExp |
  UnaryExpression |
  ParenthesizedExpression |
  BinaryExpression |
  CallExpression |
  MemberExpression
}

//!expression_misc

ParenthesizedExpression { "(" expression ")" }

ArgList { "(" commaSep<"..."? expression> ")" }

//!operators

UnaryExpression {
  (kw<"void"> | kw<"typeof"> | kw<"delete"> | LogicOp<"!"> | ArithOp<"+" | "-">)
  expression
}

BinaryExpression {
  expression !times (ArithOp<"/"> | ArithOp<"%"> | ArithOp<"*">) expression |
  expression !plus ArithOp<"+" | "-"> expression |
  expression !rel CompareOp expression
}


//!commaSep

commaSep<content> {
  (content ("," content)*)?
}


Literal { String | Number }

kw<term> { @specialize[@name={term}]<non_global_identifer, term> }

//!semi

semi { ";" }

//!skip

@skip { spaces | newline | LineComment | BlockComment }

@skip {} {
  BlockComment { "/*" (blockCommentContent | blockCommentNewline)* blockCommentEnd }
}

@local tokens {
  blockCommentEnd { "*/" }
  blockCommentNewline { "\n" }
  @else blockCommentContent
}

FnName { identifier }

ProgName { identifier }

identifier { non_global_identifer | global_identifer }

LocalVariableName { non_global_identifer }
SharedVariableName { global_identifer }

@tokens {

  ImportSlash { "/" }
  
  // Identifiers
  identifierChar { @asciiLetter | $[_$\u{a1}-\u{10ffff}] }
  identifierTail { (identifierChar | @digit)* }
  
  non_global_identifer { $[a-z] identifierTail }
  global_identifer { $[A-Z] identifierTail }

//!numberTokens

  hex { @digit | $[a-fA-F] }
  
    Number {
    (@digit ("_" | @digit)* ("." ("_" | @digit))? | "." @digit ("_" | @digit)*)
      (("e" | "E") ("+" | "-")? ("_" | @digit)+)? |
    @digit ("_" | @digit)* "n" |
    "0x" (hex | "_")+ "n"? |
    "0b" $[01_]+ "n"? |
    "0o" $[0-7_]+ "n"?
  }

  @precedence { Number "." }

  Type { "bool" | "int" | "float" | "string" }

//!operatorTokens

  ArithOp<expr> { expr }
  LogicOp<expr> { expr }

  UpdateOp { $[+\-/%*]? "=" }

  CompareOp { ("<" | ">" | "==" | "!=") }

//!regexpTokens

  RegExp { "/" (![/\\\n[] | "\\" ![\n] | "[" (![\n\\\]] | "\\" ![\n])* "]")+ ("/" $[gimsuy]*)? }

//!literalTokens

  "=" "..." "=>"
  "(" ")" "[" "]" "{" "}"
  "." "," ";" ":"
  
//!stringTokens

  String {
    '"' (![\\\n"] | "\\" _)* '"'? |
    "'" (![\\\n'] | "\\" _)* "'"?
  }

  
//!skippedTokens

  spaces[@export] { $[\u0009 \u000b\u00a0]+ }

  newline[@export] { $[\r\n] }

  LineComment { "//" ![\n]* }
  
  @precedence { ImportSlash, "/*", LineComment, ArithOp<"/"> }

  @precedence { ImportSlash, "/*", LineComment, RegExp }
  
}
